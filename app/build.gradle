/*
 * Copyright (c) 2015. Gianluca Vegetti
 * 
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 * 
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 * 
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */

apply plugin: 'com.android.application'

android {
    compileSdkVersion 28
    buildToolsVersion "28.0.2"

    useLibrary 'org.apache.http.legacy'

    defaultConfig {
        applicationId "com.iukonline.amule.android.amuleremote"
        minSdkVersion 16
        targetSdkVersion 26
        versionCode 20
        versionName "0.6"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
        debug {
            debuggable true
        }
    }

    flavorDimensions "version"
    productFlavors {

        dev {
            dimension "version"
            applicationId "com.iukonline.amule.android.amuleremote.dev"
            versionName "0.6-DEV-" + getDate()
        }

        snapshot {
            dimension "version"
            applicationId "com.iukonline.amule.android.amuleremote.snapshot"
            versionName "0.6-SNAPSHOT-" + getDate()
        }

        target {
            dimension "version"
            applicationId "com.iukonline.amule.android.amuleremote"
            versionName "0.6-" + getDate()
        }
    }
}

static def getDate() {
    def date = new Date()
    def formattedDate = date.format('yyyyMMddHHmmss')
    return formattedDate
}

repositories {
    mavenCentral()
    google()
}

configurations.all {
    resolutionStrategy {
        cacheDynamicVersionsFor 0, 'seconds'
        cacheChangingModulesFor 0, "seconds"
    }

}

dependencies {
    implementation 'com.github.nfalco79:amule-ec:0.6.0'
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support:design:28.0.0'
    implementation 'ch.acra:acra:4.11'
    implementation 'de.keyboardsurfer.android.widget:crouton:1.8.5@aar'
    implementation 'com.github.machinarius:preferencefragment:0.1.1'
    constraints {
        implementation('com.android.support:support-v4:28.0.0') {
            because 'version 18.0.0 conflict with gradle plugin version'
        }
    }
    implementation 'com.jakewharton:butterknife:6.1.0'
    annotationProcessor 'com.jakewharton:butterknife:6.1.0'
    implementation 'joda-time:joda-time:2.8.2'
}
